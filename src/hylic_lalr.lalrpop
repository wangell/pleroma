use crate::ast;
use crate::lexer;
use crate::ast::{AstNode, BinOp, Distance, PType, CType, Hvalue, EntityDef, Module, Identifier, IdentifierTarget};

use crate::common::{HashMap, String, Box, str, FromStr, Vec, ToString};

grammar;

Comma<T>: Vec<T> = { // (1)
    <mut v:(<T> ",")*> <e:T?> => match e { // (2)
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Inoc: (String, CType) = {
  <s:Symbol> ":" <c:CType> => (s, c)
}

DataDec: (String, CType) = {
  <s:Symbol> ":" <c:CType> ";" => (s, c)
}

InocList: Vec<(String, CType)> = {
  "{" Comma<Inoc> "}" => (<>).1
}

TypeSpec: CType = {
   ":" <c: CType> => c
}

Statement : Box<AstNode> = {
   "let" <s:Symbol> <t:TypeSpec?> "=" <e:Expr> ";" => Box::new(AstNode::DefinitionNode(ast::Identifier{original_sym: String::from(s), unique_sym: String::from(""), target: IdentifierTarget::Undecided}, Box::new(e))),
   <s:Symbol> "=" <e:Expr> ";" => Box::new(AstNode::AssignmentNode(ast::Identifier{original_sym: String::from(s), unique_sym: String::from(""), target: IdentifierTarget::Undecided}, Box::new(e))),
   "↵" <e:Expr> ";" => Box::new(AstNode::Return(Box::new(e))),
   "await" <e:Expr> ";" => Box::new(AstNode::Await(Box::new(e))),
   "print" <e:Expr> ";" => Box::new(AstNode::Print(Box::new(e))),
   Expr ";" => Box::new((<>).0),
}

ExprOp: BinOp = {
    "+" => BinOp::Add,
    "-" => BinOp::Sub,
    "*" => BinOp::Mul,
    "/" => BinOp::Div,
}

Expr: AstNode = {
    <e1:Expr> <o:ExprOp> <e2:Term> => AstNode::OperatorNode(Box::new(e1), o, Box::new(e2)),
    Num => AstNode::ValueNode(Hvalue::Hu8(<> as u8)),
    <s:Symbol> "." <f:Symbol> "(" <e:Comma<Expr>> ")" => AstNode::FunctionCall(ast::FunctionCall{call_type: ast::CallType::Regular, identifier: ast::Identifier{original_sym: String::from(s), unique_sym: String::from(""), target: IdentifierTarget::Undecided}, func_name: f, arguments: e}),
    <s:Symbol> "(" <e:Comma<Expr>> ")" => AstNode::FunctionCall(ast::FunctionCall{call_type: ast::CallType::NewEntity, identifier: ast::Identifier{original_sym: String::from(s), unique_sym: String::from(""), target: IdentifierTarget::Undecided}, func_name: String::from(""), arguments: e}),
    "$" <s:Symbol> "(" <e:Comma<Expr>> ")" => AstNode::FunctionCall(ast::FunctionCall{call_type: ast::CallType::NewVat, identifier: ast::Identifier{original_sym: String::from(s), unique_sym: String::from(""), target: IdentifierTarget::Undecided}, func_name: String::from("create"), arguments: e}),
    <s:Symbol> "!" <f:Symbol> "(" <e:Comma<Expr>> ")" => AstNode::Message(ast::Identifier{original_sym: String::from(s), unique_sym: String::from(""), target: IdentifierTarget::Undecided}, String::from(f), e),
    StringToken => AstNode::ValueNode(Hvalue::PString(<>)),
    Symbol => AstNode::Identifier(Identifier{original_sym: String::from(<>), unique_sym: String::from(""), target: IdentifierTarget::Undecided}),
    <e1:Expr> "⌜" <e2:Expr> "⌟" => AstNode::Index(Box::new(e1), Box::new(e2)),
    "[" <v:Comma<Expr>> "]" => AstNode::ValueNode(Hvalue::List(v)),
}

Term: AstNode = {
    <n:Num> => AstNode::ValueNode(Hvalue::Hu8(n as u8)),
    Symbol => AstNode::Identifier(Identifier{original_sym: String::from(<>), unique_sym: String::from(""), target: IdentifierTarget::Undecided}),
}

Function: (String, Box<AstNode>) = {
    "δ" <f:Symbol> "(" <p:Comma<Inoc>> ")" <nn:ReturnType> <r:CType> "{" <b:Statement*> "}" => (f.clone(), Box::new(AstNode::Function{name: f, parameters: p, body: b, return_type: r}))
}

pub Import: AstNode = {
  "~" <s:Symbol> ";" => AstNode::ImportModule(s)
  //"►"
}

pub Module: AstNode = {
  <ims: Import*> <ed:EntityDef*> => AstNode::Module(Module {imports: ims, entity_defs: ed.into_iter().collect()})
}

EntityDef: (String, AstNode) = {
    "ε" <s:Symbol> <i:InocList> "{" <d:DataDec*> <f:Function*> "}"=> (s.clone(), AstNode::EntityDef(EntityDef{name: String::from(s), inoculation_list: i, data_declarations: d, functions: f.into_iter().collect(), foreign_functions: HashMap::new()}))
}

PType: PType = {
  "u32" => PType::Pu32,
  "[" <c:CType> "]" => PType::List(Box::new(c)),
  <s:EntityIdentifier> => PType::Entity(s)
}

CType: CType = {
  "far" <base_type:PType> => CType::Loc(<>),
  "loc" <base_type:PType> => CType::Far(<>),
  "aln" <base_type:PType> => CType::Alien(<>),
  <base_type: PType> => CType::Loc(<>),
  "void" => CType::Void
}

PDist: Distance = {
  "far" => Distance::Far,
  "loc" => Distance::Local
}

Num: u8 = {
    <Hu8> => <>
};

extern {
    type Location = usize;
    type Error = lexer::LexicalError;

    enum lexer::Tok {
        " " => lexer::Tok::Space,
        ":" => lexer::Tok::Colon,
        ";" => lexer::Tok::Semicolon,
        "{" => lexer::Tok::LeftBracket,
        "}" => lexer::Tok::RightBracket,
        "let" => lexer::Tok::Let,
        "=" => lexer::Tok::Equals,

        "↵" => lexer::Tok::ReturnKeyword,
        "+" => lexer::Tok::Plus,
        "-" => lexer::Tok::Minus,
        "*" => lexer::Tok::Times,
        "/" => lexer::Tok::Divide,

        "(" => lexer::Tok::LeftParen,
        ")" => lexer::Tok::RightParen,

        "⌜" => lexer::Tok::IndexLeft,
        "⌟" => lexer::Tok::IndexRight,

        "$" => lexer::Tok::VatSym,

        "[" => lexer::Tok::LeftSquareBracket,
        "]" => lexer::Tok::RightSquareBracket,
        "\"" => lexer::Tok::QuotationMark,

        "ε" => lexer::Tok::Entity,
        "δ" => lexer::Tok::Function,
        ReturnType => lexer::Tok::ReturnType,
        "~" => lexer::Tok::Import,
        "," => lexer::Tok::Comma,
        "!" => lexer::Tok::Message,
        "." => lexer::Tok::Dot,

        "\t" => lexer::Tok::Tab,
        "\n" => lexer::Tok::Linefeed,
        EntityIdentifier => lexer::Tok::EntityIdentifier { value: <String> },
        Symbol => lexer::Tok::Symbol { value: <String> },
        StringToken => lexer::Tok::StringToken { value: <String> },
        "[0-9]+" => lexer::Tok::Number,

        "u8" => lexer::Tok::Pu8,
        "u16" => lexer::Tok::Pu16,
        "u32" => lexer::Tok::Pu32,
        "u64" => lexer::Tok::Pu64,

        "s8" => lexer::Tok::Ps8,
        "s16" => lexer::Tok::Ps16,
        "s32" => lexer::Tok::Ps32,
        "s64" => lexer::Tok::Ps64,

        "far" => lexer::Tok::Far,
        "loc" => lexer::Tok::Loc,
        "aln" => lexer::Tok::Aln,

        "await" => lexer::Tok::Await,
        "print" => lexer::Tok::Print,

        "void" => lexer::Tok::Void,
        Hu8 => lexer::Tok::Hu8 {value : <u8> },
    }
}